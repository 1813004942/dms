<?xml version="1.0" encoding="utf-8"?>
<util:MyModule xmlns:mx="http://www.adobe.com/2006/mxml"
			   xmlns:util="DMS_Util.*"
			   width="100%" height="100%" 
			   xmlns:bankcomm="http://www.bankcomm.com">
	<mx:Script>
		<![CDATA[
			import DMS_Util.ConnectParameter;
			import DMS_Util.ExtentedStringUtil;
			import DMS_Util.PaginationUtil;
			
			import com.adobe.utils.StringUtil;
			import com.bankcomm.Connect;
			import com.bankcomm.GuipPublic;
			import com.bankcomm.URLParameter;
			import com.bankcomm.dms.bo.DemoBO;
			import com.bankcomm.dms.cnst.InterfaceModeConst;
			import com.bankcomm.dms.modules.demo.componets.DemoDetail;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ItemClickEvent;
			
			private function search():void{
				gridPage.param = function():Object{
					var PARAMS:Object = new Object();
					
					if(keyInput.text != null && StringUtil.trim(keyInput.text) != ""){
						PARAMS["keyword"] = ExtentedStringUtil.symbolTranslation(StringUtil.trim(keyInput.text));
					}
					return PARAMS;
				};
				PaginationUtil.gridPageInit(demoGrid,gridPage);
			}
			
			private function itemClickHander(event:ItemClickEvent):void{
				switch (event.index){
					case 0:
						addHandler();
						break;
					case 1:
						updateHandler();
						break;
					case 2:
						deleteHandler();
						break;
					case 3:
						break;
				}
			}
			
			//新增
			private function addHandler():void{
				var detail:DemoDetail= new DemoDetail();
				detail.title="新增项目";
				detail.MODE = InterfaceModeConst.MODE_ADD;
				detail.addEventListener("refresh",function():void{
					search();
					detail.dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
				});
				GuipPublic.popUp(detail);
			}
			
			//修改
			private function updateHandler():void{
				if(!demoGrid.selectedItems || demoGrid.selectedItems.length != 1){
					Alert.show("请选择一条记录进行修改！","提示");
					return;
				}
				var detail:DemoDetail= new DemoDetail();
				detail.title="修改项目";
				detail.MODE = InterfaceModeConst.MODE_UPDATE;
				detail.demoId = demoGrid.selectedItem["demoId"];
				detail.addEventListener("refresh",function():void{
					search();
					detail.dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
				});
				GuipPublic.popUp(detail);
			}
			
			//删除项目
			private function deleteHandler():void{
				if(!demoGrid.selectedItems || demoGrid.selectedItems.length < 1){
					Alert.show("请选择一条或多条记录进行删除！","提示");
					return;
				}
				
				var callbackFunc:Function = function():void{
					var params:URLParameter = new URLParameter();
					var demoIdList:Array = new Array();
					for each(var item:Object in demoGrid.selectedItems){
						demoIdList.push(item["demoId"]);
					}

					
					params.reqBody = {"demoIdList":demoIdList};
					
					Connect.request(new ConnectParameter({url:"deleteDemo.ajax",params:params.toParameter(),callback:function(r:Object,e:Event):void{
						search();
					},isMaskAvailable:true
					}));
				};
				
				Alert.show("是否确认删除？","确认",3,null,function(event:CloseEvent):void{
					if(event.detail == Alert.YES){
						if(null != callbackFunc){
							callbackFunc();
						}
					}
				},null,2);
				
			}
			
		]]>
	</mx:Script>
	
	<mx:Array id="btnDp">
		<mx:Object label="新增" icon="@Embed('assets/page_add.png')"/>
		<mx:Object label="修改" icon="@Embed('assets/page_edit.png')"/>
		<mx:Object label="删除" icon="@Embed('assets/page_delete.png')"/>
	</mx:Array>
	<mx:HBox width="100%" horizontalGap="0" paddingTop="5" paddingBottom="2">
		<mx:ButtonBar id="bar" dataProvider="{btnDp}" itemClick="itemClickHander(event)"/>
		<mx:Spacer width="100%"/>
		
		<util:MySearchTextInput id="keyInput" width="150" prompt="项目名称/代码"
								txChange="keyInput.showClearButton = (keyInput.text != '')"
								clear="keyInput.showClearButton = false;search()" enter="search()"
								showClearButton="false" maxChars="50"/>
		<mx:Button width="23" height="23" click="search()" cornerRadius="0" toolTip="搜索"
				   icon="@Embed('assets/magnifier.png')"/>
	</mx:HBox>
	<bankcomm:DoubleClickCheckDataGrid id="demoGrid" width="100%" height="100%"
											 creationComplete="search()"
											 dataProvider="{gridPage.dataProvider}">
		<bankcomm:columns>
			<mx:DataGridColumn dataField="demoId" headerText="ID"/>
			<mx:DataGridColumn dataField="demoName" headerText="名称"/>
			<mx:DataGridColumn dataField="demoNo" headerText="代码"/>
		</bankcomm:columns>
	</bankcomm:DoubleClickCheckDataGrid>
	<util:GridPage id="gridPage" dataUrl="queryDemoList.ajax"/>
</util:MyModule>
